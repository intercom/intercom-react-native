 def packageJsonFile = file('../package.json')
 def packageJson = new groovy.json.JsonSlurper().parseText(packageJsonFile.text)
 def packageVersion = '"' + packageJson.version + '"'


buildscript {
    if (project == rootProject) {
        repositories {
            google()
            mavenCentral()
        }

        dependencies {
            classpath 'com.android.tools.build:gradle:3.5.3'
        }
    }
}

apply plugin: 'com.android.library'

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

def intercomSdkVersion = packageJson.sdkVersions.android.intercom
def targetSdk = packageJson.sdkVersions.android.targetSdk
def compileSdk = packageJson.sdkVersions.android.compileSdk
def minSdk = packageJson.sdkVersions.android.minSdk

android {
    // Compatibility for AGP v. <4.2/Gradle 8
    def agpVersion = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION.tokenize('.')[0].toInteger()
    if (agpVersion >= 7) {
      namespace 'com.intercom.reactnative'
    }

    compileSdkVersion safeExtGet('IntercomReactNative_compileSdkVersion', compileSdk)
    defaultConfig {
        minSdkVersion safeExtGet('IntercomReactNative_minSdkVersion', minSdk)
        targetSdkVersion safeExtGet('IntercomReactNative_targetSdkVersion', targetSdk)
        versionCode 1
        versionName "1.0"
        buildConfigField 'String', 'INTERCOM_VERSION_NAME', packageVersion
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    lintOptions {
        disable 'GradleCompatible'
    }

    if (agpVersion < 8) {
      compileOptions {
          sourceCompatibility JavaVersion.VERSION_1_8
          targetCompatibility JavaVersion.VERSION_1_8
      }
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
        url("$rootDir/../node_modules/react-native/android")
    }
    google()
}

dependencies {
    //noinspection GradleDynamicVersion
    implementation "com.facebook.react:react-native:+"  // From node_modules
    implementation "com.google.firebase:firebase-messaging:${safeExtGet('firebaseMessagingVersion', '20.2.+')}"
    implementation "io.intercom.android:intercom-sdk:${intercomSdkVersion}"
}
