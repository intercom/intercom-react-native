 def packageJsonFile = file('../package.json')
 def packageJson = new groovy.json.JsonSlurper().parseText(packageJsonFile.text)
 def packageVersion = '"' + packageJson.version + '"'


buildscript {
    if (project == rootProject) {
        repositories {
            google()
            mavenCentral()
        }

        dependencies {
            classpath 'com.android.tools.build:gradle:3.5.3'
        }
    }
}

apply plugin: 'com.android.library'

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
    // Compatibility for AGP v. <4.2
    namespace 'io.intercom.android'

    compileSdkVersion safeExtGet('IntercomReactNative_compileSdkVersion', 34)
    defaultConfig {
        minSdkVersion safeExtGet('IntercomReactNative_minSdkVersion', 21)
        targetSdkVersion safeExtGet('IntercomReactNative_targetSdkVersion', 34)
        versionCode 1
        versionName "1.0"
        buildConfigField 'String', 'INTERCOM_VERSION_NAME', packageVersion
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    lintOptions {
        disable 'GradleCompatible'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
        url("$rootDir/../node_modules/react-native/android")
    }
    google()
}

dependencies {
    //noinspection GradleDynamicVersion
    implementation "com.facebook.react:react-native:+"  // From node_modules
    implementation "com.google.firebase:firebase-messaging:${safeExtGet('firebaseMessagingVersion', '20.2.+')}"
    implementation 'io.intercom.android:intercom-sdk:15.4.0'
}
